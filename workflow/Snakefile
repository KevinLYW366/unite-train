# Main entrypoint of the workflow. 
# Please follow the best practices: 
# https://snakemake.readthedocs.io/en/stable/snakefiles/best_practices.html,
# in particular regarding the standardized folder structure mentioned there. 

from snakemake.io import glob_wildcards, expand

configfile: "config/config.yaml"

GOALS = expand("results/unite_{ver}_{id}_{type}{date}-Q2-{q2v}.qza",
               ver=config["version"],
               id=config["cluster"],
               type=config["type"],
               date=config["date"],
               q2v=config["q2v"]
               )
# print(GOALS)
Q2CONDA = config["q2env"]

rule all:
    input:
        #taxa="results/sh_taxa_qiime_{ver}_{id}_{type}{date}_dev.qza",
        #seqs="results/sh_refs_qiime_{ver}_{id}_{type}{date}_dev.qza"
        GOALS

checkpoint download: # This is not proper snakemake. I can't get my tuples to work.
    output:
        #ref=temp("downloads/sh_refs_qiime_{ver}_{id}_{type}{date}_dev.fasta"),
        #tax=temp("downloads/sh_taxonomy_qiime_{ver}_{id}_{type}{date}_dev.txt")
        # Note the messy wildcards in rule.reformat_seqs and rule.import_tax this requires
        dir=directory("downloads/")
    # conda: # TODO: troubleshoot this and pin wget and tar versions
    #      "envs/unix.yaml"
    log: "logs/downloads.log"
    conda: "envs/unix-tools.yaml"
    threads: 1000
    shell:
        """
        mkdir -p downloads

        # Version 9 update. From DOIs, get download URLs from here:
        # https://api.plutof.ut.ee/v1/public/dois/?format=api&identifier=10.15156/BIO/2483915
        # All these DOIs now link to three files:
        # - sh_qiime_release_16.10.2022.tgz
        # - sh_qiime_release_27.10.2022.tgz
        # - sh_qiime_release_29.11.2022.tgz
        # These are dates. I can't find release information.

        # 9.0	2022-10-16	Fungi	17 495	143 840	Current	https://doi.org/10.15156/BIO/2483915
        wget -qO- https://files.plutof.ut.ee/public/orig/59/12/591225E8985EFC44B595C79AF5F467421B4D9A95093A0811B13CB4CC13A6DA46.tgz | \
          tar xz -C downloads --strip-components 1 # sh_qiime_release_29.11.2022.tgz      # normal

        # 9.0	2022-10-16	Fungi	17 495	188 070	Current	https://doi.org/10.15156/BIO/2483916
        wget -qO- https://files.plutof.ut.ee/public/orig/67/1C/671C4D441E50DCD30691B84EED22065D77BAD3D18AF1905675633979BF323754.tgz | \
          tar xz -C downloads --strip-components 1 # sh_qiime_release_s_29.11.2022.tgz    # add s for 97% singletons

        # 9.0	2022-10-16	All eukaryotes	17 683	216 528	Current	https://doi.org/10.15156/BIO/2483917
        wget -qO- https://files.plutof.ut.ee/public/orig/8F/FC/8FFCC8A730E50FEEF8CFFEEFEF02A22FBCF7E02B7FD31C6649754834D2CB0E6F.tgz | \
          tar xz -C downloads --strip-components 1 # sh_qiime_release_all_29.11.2022.tgz  # add all for Euks

        # 9.0	2022-10-16	All eukaryotes	17 683	308 588	Current	https://doi.org/10.15156/BIO/2483918
        wget -qO- https://files.plutof.ut.ee/public/orig/71/E6/71E68A5DD3F5C311F913757F9E7CA2508430F9400C14413ADA08A0BB33169BB4.tgz | \
          tar xz -C downloads --strip-components 1 # sh_qiime_release_s_all_29.11.2022.tgz # and s and all for 97% Euks singletons

        """

rule reformat_seqs:
    input: "downloads"
    output: temp("results/sh_refs_qiime_{ver}_{id}_{type}{date}_dev.fixed.fasta")
    log: "logs/reforamt_{ver}_{id}_{type}{date}.log"
    conda: "envs/unix-tools.yaml"
    shell: "awk '/^>/ {{print($0)}}; /^[^>]/ {{print(toupper($0))}}' \
            {input}/sh_refs_qiime_{wildcards.ver}_{wildcards.id}_{wildcards.type}{wildcards.date}_dev.fasta | \
            tr -d ' ' > {output}"

rule import_seqs:
    input:  "results/sh_refs_qiime_{ver}_{id}_{type}{date}_dev.fixed.fasta"
    output: temp("results/sh_refs_qiime_{ver}_{id}_{type}{date}_dev.qza")
    log: "logs/import_seqs_{ver}_{id}_{type}{date}.log"
    conda: Q2CONDA
    shell: "qiime tools import --type FeatureData[Sequence] \
            --input-path {input} \
            --output-path {output}"

rule import_tax:
    input: "downloads"
    output:temp("results/sh_taxa_qiime_{ver}_{id}_{type}{date}_dev.qza")
    log: "logs/import_taxa_{ver}_{id}_{type}{date}.log"
    conda: Q2CONDA
    shell: "qiime tools import --type FeatureData[Taxonomy] \
            --input-format HeaderlessTSVTaxonomyFormat \
            --input-path {input}/sh_taxonomy_qiime_{wildcards.ver}_{wildcards.id}_{wildcards.type}{wildcards.date}_dev.txt \
            --output-path {output}"

rule train:
    input:
        ref=rules.import_seqs.output,
        tax=rules.import_tax.output
    output: protected("results/unite_{ver}_{id}_{type}{date}-Q2-{q2v}.qza")
    log: "logs/train_{ver}_{id}_{type}{date}-Q2-{q2v}.log"
    conda: Q2CONDA
    benchmark: "logs/train_{ver}_{id}_{type}{date}-Q2-{q2v}.tsv"
    resources:
        mem_mb=9000
    shell: "qiime feature-classifier fit-classifier-naive-bayes \
            --p-classify--chunk-size 10000 \
            --i-reference-reads    {input.ref} \
            --i-reference-taxonomy {input.tax} \
            --o-classifier {output}"
